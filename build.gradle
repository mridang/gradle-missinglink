import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id "jacoco"
	id "pmd"
	id "checkstyle"
    id 'maven-publish'
	id 'com.gradle.plugin-publish' version '2.0.0'
    id 'com.diffplug.spotless' version '8.2.0'
    id "net.ltgt.errorprone" version "4.4.0"
	id "net.ltgt.nullaway" version "2.4.0"
    id "com.github.spotbugs" version "6.4.8"
}

group = 'io.github.mridang.gradle'
version = project.findProperty("version") ?: "0.0.0"

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

javadoc.options.addStringOption('Xdoclint:none')

// Configuration for the Spotbugs plugin. It seems that it isn't possible to define
// the output report formats here and those seem to be defined on the individual
// task level.
spotbugs {
	effort = Effort.MAX
	showProgress = true
}

tasks.withType(SpotBugsTask).configureEach { task ->
	task.reports {
		sarif {
			required = true
		}
		xml {
			required = true
		}
		html {
			required = true
		}
	}
}

// Configuration for the Checkstyle plugin. It seems that it isn't possible to define
// the output report formats here and those seem to be defined on the individual
// task level.
checkstyle {
	config = rootProject.resources.text.fromFile("gradle/config/checkstyle/google_checks.xml")
}

tasks.withType(Checkstyle).configureEach { task ->
	task.enabled = true
	task.reports {
		sarif {
			required = true
		}
		xml {
			required = true
		}
		html {
			required = true
		}
	}
}

//// Configuration for the nullaway extension. The rest of the parameters must be
//// specified on the java-compile tasks. The only option that be specified here
//// is the name of the root package to be analysed.
nullaway {
	annotatedPackages.add("io.github.mridang")
}

// Configuration for the Jacoco plugin. It seems that it isn't possible to define
// the output report formats here and those seem to be defined on the individual
// task level.
jacoco {

}

// We only need the XML report so that the CI tools can parse it. The HTML isn't needed.
jacocoTestReport {
	reports {
		csv {
			required = true
			destination = file("$buildDir/reports/jacoco/test/jacoco.csv")
		}
		xml {
			required = true
			destination = file("$buildDir/reports/jacoco/test/jacoco.xml")
		}
		html {
			required = true
		}
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
	reports {
		junitXml {
			required = true
			destination = file("$buildDir/reports/tests/test")
		}
		html {
			required = true
		}
	}
	systemProperty 'java.awt.headless', 'true'
	maxParallelForks = (int) (Runtime.runtime.availableProcessors() / 2 + 1)
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

spotless {
	java {
		target '**/*.java'
		targetExclude 'buildSrc/build/**/*'
		googleJavaFormat()
	}
}

dependencies {
	checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: '13.0.0'
    errorprone group: 'com.uber.nullaway', name: 'nullaway', version: '0.13.0'
	errorprone group: 'com.google.errorprone', name: 'error_prone_core', version: '2.46.0'
	spotbugsPlugins group: 'com.h3xstream.findsecbugs', name: 'findsecbugs-plugin', version: '1.14.0'
    spotbugs group: 'com.github.spotbugs', name: 'spotbugs', version: '4.9.8'
	jacocoAgent group: 'org.jacoco', name: 'org.jacoco.agent', version: '0.8.14'
	jacocoAnt group: 'org.jacoco', name: 'org.jacoco.ant', version: '0.8.14'
    implementation gradleApi()
    implementation group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'
    implementation group: 'com.spotify', name: 'missinglink-core', version: '0.2.11'
    implementation("com.fasterxml.jackson.core:jackson-databind:2.21.0")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.21.0")
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '6.0.2'
}

gradlePlugin {
	website = "https://github.com/mridang/gradle-missinglink"
	vcsUrl = "https://github.com/mridang/gradle-missinglink"
    plugins {
        missingLink {
            id = 'io.github.mridang.gradle.missinglink'
            implementationClass = 'io.github.mridang.gradle.missinglink.MissingLinkPlugin'
            displayName = 'Gradle MissingLink Plugin'
            description = 'A Gradle plugin that checks for dependency conflicts using Spotify MissingLink'
			tags.set([
				'dependencies',
				'classpath',
				'verfication',
			])
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
